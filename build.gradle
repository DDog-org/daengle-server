plugins {
    id 'java'
    id 'org.springframework.boot' version '3.3.5'
    id 'io.spring.dependency-management' version '1.1.6'
    id 'org.jetbrains.kotlin.jvm' version '1.9.25' // Kotlin
    id 'org.jetbrains.kotlin.plugin.spring' version '1.9.25'
}

bootJar {
    enabled = false
}

allprojects {
    group = 'ddog'
    version = '0.0.1-SNAPSHOT'

    repositories {
        mavenCentral()
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'org.jetbrains.kotlin.jvm'
    apply plugin: 'org.jetbrains.kotlin.plugin.spring'

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(17)
        }
    }

    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
    }

    dependencies {  //TODO 모듈별로 의존성 분산하기
        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'
        testImplementation 'org.springframework.boot:spring-boot-starter-test'

        //Spring Security   //TODO API Gate WAY 를 활용하여 auth 모듈에서만 시큐리티 의존하도록 추후 변경
        implementation 'org.springframework.boot:spring-boot-starter-security'
        testImplementation 'org.springframework.security:spring-security-test'

        //jwt
        implementation 'io.jsonwebtoken:jjwt-api:0.11.5'
        implementation 'io.jsonwebtoken:jjwt-impl:0.11.5'
        implementation 'io.jsonwebtoken:jjwt-jackson:0.11.5'

        //OAuth
        implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'

        implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    }

    test {
        useJUnitPlatform()
    }
}